package com.sample

import com.healthriskassessment.model.Desease;
import com.healthriskassessment.dto.UserRiskDTO;
import com.healthriskassessment.dto.UserDeseaseDTO;
import java.util.List;
import java.util.Set;


rule "Find deseases"
	agenda-group "deseases"
    when
        $desease: Desease($deseaseRisks: risks, $deseaseName: name, $deseaseDescription: description)
        $healthData: HealthDataDTO($userRisks: risks)
        $deseaseRiskNames: List() from accumulate (
            Risk($name: name) from $deseaseRisks, collectList($name)
        )
        checkForDeseaseRisks($num, $matchingRisks, $deseaseRiskNames, $userRisks;)
    then
    	System.out.println("\nFind deseases fired for desease: " + $deseaseName);
    	UserDeseaseDTO UserDeseaseDTO = new UserDeseaseDTO($deseaseName, $deseaseDescription, null, $matchingRisks);
        $healthData.getDeseases().add(UserDeseaseDTO);
end

query checkForDeseaseRisks(Number $num, Set $matchingRisks,List deseaseRiskNames, List userRisks)
    $matchingRisks := Set(size > 0) from accumulate (
     	UserRiskDTO($risk: this, $name: riskName, riskName memberOf deseaseRiskNames) from userRisks,
     	collectSet($risk)
    )

end

// **************************************************************************
//						DESEASE AT RISK CLASSIFICATION
// **************************************************************************

rule "Desease classification"
	agenda-group "deseases"
	enabled false
	when
		$healthData: HealthDataDTO($deseases: deseases, $ds: deseases.size) @Watch(deseases)
		$deseaseAtRisk: UserDeseaseDTO(riskLevel == null, $deseaseName: deseaseName,
			$userDeseaseRisks: risks ) from $deseases // all risks that the user has
		$desease: Desease(name == $deseaseName, $allDeseaseRisks: risks) // all risks that may affect this disease
	then
end

rule "Desease at risk classification high risk" extends "Desease classification"
	agenda-group "deseases"
	when
		checkNumberOfRisks($allDeseaseRisks, $userDeseaseRisks;)
	then
	    System.out.println("\nDesease at risk classification high risk fired for: " + $deseaseName);
	    $deseaseAtRisk.setRiskLevel(RiskLevel.HIGH);
		//modify($healthData) { }
end

// check if the user has all the risks that affect a disease / or one risk is missing
// desesaseRisks - all risks that may affect this disease
// userRisks - all risks that the user has and are related to this desease
query checkNumberOfRisks(Set deseaseRisks, Set userRisks)

 	$deseaseRiskNames := List() from accumulate (
            Risk($name: name) from deseaseRisks, collectList($name)
        )
 
    $num := Number(intValue >= $deseaseRiskNames.size() - 1 ) from accumulate(
        UserRiskDTO($name: riskName, riskName memberOf $deseaseRiskNames) from userRisks,
        init(int count = 0;),
        action(count += 1;),
        result(count)
    )
end

rule "Desease at risk classification high risk 2" extends "Desease classification"
	agenda-group "deseases"
	when
		checkNumberOfHighRisks($userDeseaseRisks, 2;)
	then
	    System.out.println("\nDesease at risk classification high risk 2 fired for: " + $deseaseName);
		$deseaseAtRisk.setRiskLevel(RiskLevel.HIGH);
		//modify($healthData) { }
end

// check the number of user risks for one desease marked as high risk
// userRisks - all risks that the user has and are related to one desease
query checkNumberOfHighRisks(Set userRisks, int lowerLimit)

 		$highRisks: Number(intValue > lowerLimit) from accumulate(
			UserRiskDTO(riskLevel == RiskLevel.HIGH) from userRisks,
        		init(int count = 0;),
        		action(count += 1;),
        		result(count)		
        )

end

rule "Desease at risk classification medium risk " extends "Desease classification"
	agenda-group "deseases"
	when	
        Number(intValue > 3) from $ds
        not checkNumberOfHighRisks($userDeseaseRisks, 1;)
		not checkNumberOfRisks($allDeseaseRisks, $userDeseaseRisks;)     
	then
	    System.out.println("\nDesease at risk classification medium risk fired for: " + $deseaseName);
		$deseaseAtRisk.setRiskLevel(RiskLevel.MEDIUM);
		//modify($healthData) { }
end

rule "Desease at risk classification low risk " extends "Desease classification"
	agenda-group "deseases"
	when	
        Number(intValue < 4) from $ds
        not checkNumberOfHighRisks($userDeseaseRisks, 2;)
		not checkNumberOfRisks($allDeseaseRisks, $userDeseaseRisks;)     
	then
	    System.out.println("\nDesease at risk classification low risk fired for: " + $deseaseName);
		$deseaseAtRisk.setRiskLevel(RiskLevel.LOW);
		//modify($healthData) { }
end
